import datetime

Import("env")

SConscript("Controllers/SConscript")

if env["SCONS_STAGE"] == "build" :
    if not GetOption("help") and not env.get("HAVE_QT", 0) :
        if "Swift" in env["PROJECTS"] :
            print("Warning: Swift requires Qt. Not building the Swift Qt application.")
            env["PROJECTS"].remove("Swift")
    elif not GetOption("help") and env["target"] == "native" and "Swift" in env["PROJECTS"] :
        try :
            SConscript("QtUI/SConscript")
        except Exception as e:
            print("Warning: %s" % str(e))
            env["PROJECTS"].remove("Swift")
if "Swift" in env["PROJECTS"] and env["BOOST_1_64_DETECTED"] and not env.get("allow_boost_1_64") and not env.GetOption("clean") :
    #Version 1.64 has some issues with the serialization of boost::optional, see https://svn.boost.org/trac10/ticket/13050
    print("Boost 1.64 has been detected. It is not recommended to use this version due to a regression within the library. Swift has been removed from the current build. You can still use this version by setting allow_boost_1_64 to true, but recent chats and highlighting rules will reset.")
    env["PROJECTS"].remove("Swift")
if env["help2man"]:
    SConscript("Packaging/SConscript")